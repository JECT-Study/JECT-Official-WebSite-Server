plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'org.ject'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release' }
}

jacoco {
    toolVersion = "0.8.12"
}

dependencies {
    // Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // DevTools
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
//    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // AWS DynamoDB
    implementation(platform("software.amazon.awssdk:bom:2.30.11"))
    implementation 'software.amazon.awssdk:dynamodb-enhanced'
    implementation 'com.amazonaws:DynamoDBLocal:2.5.4'
    implementation 'io.github.boostchicken:spring-data-dynamodb:5.2.5'
    implementation 'io.awspring.cloud:spring-cloud-aws-dynamodb:3.3.0'

    // AWS S3
    implementation 'software.amazon.awssdk:s3'

    // SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'software.amazon.awssdk:sesv2'

    // Query DSL
    implementation 'com.querydsl:querydsl-apt:5.0.0'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    implementation 'com.querydsl:querydsl-core:5.0.0'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.awaitility:awaitility-scala:4.2.1'
    testImplementation 'org.springframework.security:spring-security-test'

    //h2
    testImplementation 'com.h2database:h2'

    // TestContainers
    testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
    testImplementation 'org.testcontainers:mysql:1.20.4'
    testImplementation 'org.testcontainers:rabbitmq:1.20.4'
    testImplementation 'com.redis:testcontainers-redis:2.2.2'
    implementation 'org.testcontainers:testcontainers-bom:1.20.2'
    implementation 'org.testcontainers:testcontainers'


    //p6spy
    implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.2"

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // jackon
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'

    // monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // logback
    implementation "ca.pjer:logback-awslogs-appender:1.6.0"
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.0")
    implementation 'io.awspring.cloud:spring-cloud-aws-starter'

    // Bucket4j
    implementation 'com.bucket4j:bucket4j-core:8.10.1'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Slack
    implementation 'com.slack.api:slack-api-client:1.45.3'
}

configurations {
    compileJava{
        extendsFrom annotationProcessor
    }
    querydsl.extendsFrom compileClasspath
}

test {
    useJUnitPlatform(){
        // throughput test 제외
        excludeTags 'throughput'
    }
    // test profile로 테스트
    systemProperty 'spring.profiles.active', 'test'

    // 테스트 종료 후 jacoco 실행
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/exception/**',
                            '**/dto/**'
                    ])
                })
        )
    }

    finalizedBy(jacocoTestCoverageVerification)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true // 규칙 활성화

            limit {
                counter = "LINE" // 라인 커버리지
                value = "COVEREDRATIO"
                minimum = 0.60  // 최소 70% 이상 만족
            }

            excludes = [
                    '*.exception.*',
                    '*.dto.*'
            ]
        }
    }
}

def generated = 'src/main/generated'

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = configurations.querydsl
    options.compilerArgs += '--enable-preview'
}

sourceSets {
    main.java.srcDirs += [ generated ]
}

jar{
    enabled = false
}

bootJar {
    archiveFileName = "ject-home.jar"
}

clean {
    delete file(generated)
}
